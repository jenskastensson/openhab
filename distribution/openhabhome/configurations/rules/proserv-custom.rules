import org.openhab.core.library.types.*
import org.openhab.core.persistence.*
import org.openhab.model.script.actions.*

rule "PhasesTotal"
when
   Item  itemProServLog3 received update or
   Item  itemProServLog4 received update or
   Item  itemProServLog5 received update 
then
   var Number phaseTotal = 0
   if ( !(itemProServLog3.state instanceof UnDefType) ) {
      phaseTotal = itemProServLog3.state as DecimalType
   }
   if ( !(itemProServLog4.state instanceof UnDefType) ) {
      phaseTotal = phaseTotal + itemProServLog4.state as DecimalType
   }
   if ( !(itemProServLog5.state instanceof UnDefType) ) {
      phaseTotal = phaseTotal + itemProServLog5.state as DecimalType
   }
   postUpdate(itemNumber1,phaseTotal)
end

rule "Update Temperature Min- and Max values"
when
    Item  itemProServLog38 received update
    
then
    var Number Min
    var Number Max
    var String tmp
    var SimpleDateFormat df = new SimpleDateFormat( "HH:mm" ) 
    
    if (itemProServLog38.state instanceof DecimalType) {
        Min = (itemProServLog38.minimumSince(now.toDateMidnight, "rrd4j").state as DecimalType)
        postUpdate(TempMinOutside, Min)
        tmp = (Math::round(Min.floatValue*10.0)/10.0) + " °C (" + df.format(itemProServLog38.minimumSince(now.toDateMidnight, "rrd4j").timestamp) + " )"
        postUpdate(Temperature_Garden_Min_Formatted, tmp)

        Max = itemProServLog38.maximumSince(now.toDateMidnight, "rrd4j").state as DecimalType
        postUpdate(TempMaxOutside, Max)
        df = new SimpleDateFormat( "HH:mm" ) 
        tmp = (Math::round(Max.floatValue*10.0)/10.0) + " °C (" + df.format(itemProServLog38.maximumSince(now.toDateMidnight, "rrd4j").timestamp) + ")"
        postUpdate(Temperature_Garden_Max_Formatted, tmp)
    }
end